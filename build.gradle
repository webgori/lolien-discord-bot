import com.github.spotbugs.SpotBugsTask

plugins {
    id "java"
    id "war"
    id "checkstyle"
    id "com.github.spotbugs" version "3.0.0"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
}

group = 'kr.webgori'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

war {
    archiveFileName = 'ROOT.war'
}

spotbugs {
    toolVersion = '3.1.12'
}

tasks.withType(SpotBugsTask) {
    reports.xml.enabled = false
    reports.html.enabled = true
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url = "https://jitpack.io" }
}

ext {
    set('springBootVersion', "2.0.9.RELEASE")
    set('springCloudDependencyVersion', "Finchley.SR4")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
    }
}

sourceSets {
    // we define `main` sourceSet here, so SpotBugs Gradle Plugin generates `spotbugsMain` task
    main {
        java.srcDirs = ['src/main/java']
    }
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    compileOnly 'org.projectlombok:lombok:1.18.12'

    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-data-redis')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-quartz')

    implementation group: 'at.stefangeyer.challonge.serializer', name: 'gson', version: '2.0.1'
    implementation group: 'at.stefangeyer.challonge.rest', name: 'retrofit', version: '2.0.1'

    implementation group: 'it.ozimov', name: 'embedded-redis', version: '0.7.2'
    implementation("net.dv8tion:JDA:3.8.3_462")
    implementation group: 'com.google.guava', name: 'guava', version: '27.1-jre'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.3'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    implementation group: 'org.javassist', name: 'javassist', version: '3.27.0-GA'
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.5.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation group: 'com.github.taycaldwell', name: 'riot-api-java', version: '4.3.0'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.11.3'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.2.30'

    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudDependencyVersion}"
    }
}

tasks.withType(Test) {
    systemProperty "file.encoding", "UTF-8"
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
        }
    }
}

checkstyle {
    toolVersion = '8.5'
}

/**
 * ignoreFailures = false 설정만으로 checkstyle 위배 시 빌드가 실패해야 하지만
 * 다음과 같은 버그가 있습니다.
 * https://github.com/gradle/gradle/issues/881
 *
 * 이 구성은 버그가 해결될 때 까지 사용되는 임시방편입니다.
 * */
tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}